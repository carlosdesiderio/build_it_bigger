apply plugin: 'com.android.application'
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'


android {
    compileSdkVersion 27
    buildToolsVersion "27.0.1"

    defaultConfig {
        applicationId "uk.me.desiderio.builditbigger"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            ext {
                // this can be set as needed during dev
                isServerLive = false
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ext {
                // never change this
                isServerLive = true
            }
        }
    }

    flavorDimensions "tier"
    productFlavors {
        free {
            applicationId "uk.me.desiderio.builditbigger.free"
            versionName "1.0-free"
        }
        paid {
            applicationId "uk.me.desiderio.builditbigger.paid"
            versionName "1.0-paid"
        }
    }

    buildTypes.each {
        if(it.ext.isServerLive) {
            it.buildConfigField 'String', 'ENDPOINT_SERVER_URL_STRING',
                    "\"https://desjokesapi.appspot.com/_ah/api/\""
        } else {
            it.buildConfigField 'String', 'ENDPOINT_SERVER_URL_STRING',
                    "\"http://10.0.2.2:8080/_ah/api/\""
        }

    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(path: ':jokedisplay')
    implementation 'com.android.support:appcompat-v7:27.1.0'

    // gc endpoint dependency
    implementation 'com.google.api-client:google-api-client:1.23.0'

    // admob dependency
    freeImplementation 'com.google.android.gms:play-services-ads:11.8.0'
    implementation 'com.google.http-client:google-http-client-android:1.23.0'

    endpointsServer project(path: ':backend_v2', configuration: 'endpoints')

    androidTestImplementation 'com.android.support.test:runner:1.0.1'
}
